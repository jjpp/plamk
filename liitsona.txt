! siia ette multicharid.

Definitions
    Täht = [ a | b | c | d | e | f | g | h | 
    	     i | j | k | l | m | n | o | p | 
	     q | r | s | š | z | ž | t | u | 
	     v | w | x | y | õ | ä | ö | ü ];
    Arv = [ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ];
    Tähtsõne = [Täht | %-]+;
    Sona = [[Arv]+ (%- | %. | %,)]* [Arv]+ (%. | %% | %' (%') ) | ([Arv]+) Tähtsõne;
    ArvJaKääne = [ %+sg | %+pl ] [ %+nom | %+gen | %+part | %+ill | %+adit 
    				  | %+in | %+el | %+all | %+ad | %+abl | %+tr 
				  | %+ter | %+es | %+ab | %+kom ];

    VerbiParadigma = "ma+V"  [
		 [ %+sup [ %+ill | %+in | %+el | %+ab | %+tr | %+imps ] ] |
		   %+inf | %+ger |
		 [ %+partic [ %+pres | %+past ] [ %+ps | %+imps ] ] |
		 [ %+indic [ %+pres | %+impf ] 
		 	[ %+imps | [ [ %+ps1 | %+ps2 | %+ps3 ] [ %+sg | %+pl ] %+ps ] ] %+af ] |
		 [ %+indic %+pres [ %+ps | %+imps ] %+neg ] |
		 [ %+quot [ %+pres [ %+ps | %+imps ] | %+past %+imps | %+partic %+past %+af ] ] |
		 [ %+cond %+pres [ %+ps1 | %+ps2 | %+ps3 ] [ %+sg | %+pl ] %+ps %+af ] |
		 [ %+cond %+partic %+past [ %+ps1 | %+ps2 | %+ps3 ] [ %+sg | %+pl ] %+af ] |
		 [ %+cond [ %+pres | %+past ] %+imps ] |
		 [ %+imper %+pres 
		 	[ %+imps | [ %+ps2 %+sg | %+ps3 %+sg %+ps %+af |
				[ %+ps1 | %+ps2 | %+ps3 ] %+pl %+ps %+af ] ] ]
    ];

!  +A +Adv +G +H +I +J +K +Num +O +Ord +Pron +S +X

    Gi = ( "+gi" );

    Omadussõna' = Sona "+A" ("+S") ([ "+comp" | "+super" ]) ( "+_us" "+S" | "+_ke" | "+_kene" );
    Omadussõna = [ Omadussõna' ArvJaKääne | Sona "+A" ];
    Nimisõna' = Sona [ ("+A") "+S" | "+H" ] ([ "+_ke" | "+_kene" ]);
    Nimisõna = Nimisõna' ArvJaKääne;
    Käänduvsõna = Omadussõna | Nimisõna | MuuKäändsõna;
    MuuKäändsõna = [ Sona [ "+G" | "+Num" | "+Ord" | "+Pron" ] ArvJaKääne ] |
			[ Sona [ "+Ord" | "+Num" ] ]; 
    Määrsõna = Sona "+Adv";
    Hüüdsõna = Sona "+I";
    Sidesõna = Sona "+J";
    Kaassõna = Sona "+K";
    Muusõna = Sona "+X";
    Genatribuut = Sona "+G";
    Tegusõna = Tähtsõne VerbiParadigma;
    Muutumatusõna = Määrsõna | Hüüdsõna | Sidesõna | Kaassõna | Muusõna | Genatribuut;

    SP = ( "&" "-" ) "&";
    PreKriips = ( "-" "&" );
    Kriips = "&" "-";

LEXICON Root
 < PreKriips > Käänduvad_sõnad;
 < PreKriips > Tegusõnad;
 Muutumatud_sõnad;


LEXICON Tegusõnad
 < Tegusõna > GI;


LEXICON Nimisõnad 
 < Nimisõna > GI;

 < Nimisõna "&" "-" "&" > Nimisõnad; ! rindliitnimisõnad, EKKR SM32

 < Nimisõna' "+sg" "+nom" SP > Tegusõnad; ! EKKR, SM19
 < Nimisõna' "+sg" "+nom" Kriips > #;

 < Nimisõna' [ "+sg" [ "+nom" | "+gen" ] | "+pl" "+gen" | "+prefix" ] SP > Nimisõnad; ! EKKR, SM27 ! FIXME: lühitüved
 < Nimisõna' [ "+sg" [ "+nom" | "+gen" ] | "+pl" "+gen" | "+prefix" ] SP > Omadussõnad; ! EKKR, SM37 ! FIXME: lühitüved
 < Nimisõna' [ "+sg" [ "+nom" | "+gen" ] | "+pl" "+gen" | "+prefix" ] SP > Määrsõnad; ! EKKR, S422 ! FIXME: lühitüved
 < Nimisõna' [ "+sg" [ "+nom" | "+gen" ] | "+pl" "+gen" | "+prefix" ] Kriips > #; 

LEXICON Omadussõnad
 < Omadussõna > GI;
 < Omadussõna' ("+sg" "+nom") SP > Tegusõnad; ! EKKR, SM19
 < Omadussõna' ("+sg" "+nom") Kriips > #;
 < Omadussõna' ("+sg" "+nom" | "+prefix") SP > Nimisõnad; ! EKKR, SM27 ! FIXME: lühitüved
 < Omadussõna' ("+sg" "+nom" | "+prefix") SP > Omadussõnad; ! EKKR, SM38 ! FIXME: lühitüved
 < Omadussõna' ("+sg" "+nom" | "+prefix") SP > Määrsõnad; ! EKKR, SM42 ! FIXME: lühitüved
 < Omadussõna' ("+sg" "+nom" | "+prefix") Kriips > #; 

LEXICON Määrsõnad
 < Määrsõna > GI;
 < Määrsõna SP > Tegusõnad; ! EKKR, SM19
 < Määrsõna ("+prefix") SP > Nimisõnad; ! EKKR, SM31
 < Määrsõna ("+prefix") SP > Omadussõnad; ! EKKR, SM39

 < Määrsõna ("+prefix") SP > Määrsõnad; ! EKKR, SM42
 < Määrsõna ("+prefix") SP Määrsõna "+sufix" > GI;

LEXICON Kaassõnad
 < Kaassõna > GI;
 < Kaassõna SP > Käänduvad_sõnad; ! EKKR, SM42 ?
 

LEXICON Käänduvad_sõnad
 Nimisõnad;
 Omadussõnad;
 < MuuKäändsõna > GI;

LEXICON Muutumatud_sõnad
 Määrsõnad;
 Kaassõnad;
 < Hüüdsõna | Sidesõna | Muusõna | Genatribuut > GI;


LEXICON GI
< Gi > #;


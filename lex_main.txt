Definitions
   Number = [%0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9]; 

!Definitions
!  C = [b|d|g|h|j|k|l|m|n|p|r|s|sh|z|zh|t|v];
!  V = [a|e|i|o|u|õ|ä|ö|ü];
!  F = [B|D|G|S];	! laadivahelduslikud konsonandid
!  Alfa = [C | V];

LEXICON Sõnastik
  Nimisõna;	!substantive
  Omadussõna;	!adjective
  Pärisnimi;	
  Tegusõna; 	!verb
  Määrsõna;	!adverb;
  Numbrid;
  Hüüdsõna;
  Sidesõna;
  Asesõna;
  Genatribuut;
  Arvsõna;
  Järgarvsõna;
  Muu_sõna;
  Kaassõna;
  Lisanduvad_erandid;
 
! tüvevariantide moodustus ja kasutus

LEXICON 01	!vallatu
An_SgN;
An_SgG;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON 02_U	!õpik
An_SgN;
:u TP_02b0;

LEXICON 02_A 	!tänav
An_SgN;
:a TP_02b0;

LEXICON 02_I	!number
An_SgN;
:i TP_02b0;

LEXICON 02_DA	!tore
An_SgN;
:da TP_02b0;

LEXICON 02_NE-SE  !raudne
:ne An_SgN;
:se TP_02b0;

LEXICON 02_S-NDA  !kümnes
:s An_SgN;
:nda TP_02b0;

LEXICON 02_S-JA  !paraja
:s An_SgN;
:ja TP_02b0;

LEXICON 02_T-NDE  !tuhat
:t An_SgN;
:nde TP_02b0;

LEXICON 02_S-0	!sipelgas
:s An_SgN;
TP_02b0;

LEXICON 03_A	!vaher
:$ An_SgN;
:a TP_03bt;

LEXICON 03_I	!manner
:$ An_SgN;
:i TP_03bt;

LEXICON 04		!ase
An_SgN;
An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_I-ME	!võti
:i$ An_SgN;
:i$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_E-ME	!seade, liige
:e$ An_SgN;
:e$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_U-ME	!mitu
:u$ An_SgN;
:u$ An_SgP_t;
:me TP_04b0-05bt;

LEXICON 05_S-0	!kallis
:s$ An_SgN;
:s$ An_SgP_t;
TP_04b0-05bt;

LEXICON 05_A	!valvas
:$ An_SgN;
:a TP_03bt;

LEXICON 06	!mõte
:$ TP_09a0;
TP_07bt;

LEXICON 07_S-0  !hammas
:S$ TP_07an;
TP_07bt;

LEXICON 08_A  !kukal
:$ TP_08an;
:a TP_07bt;

LEXICON 08_E  !tütar
:$ TP_08an;
:e TP_07bt;

LEXICON 09_E	!katus
TP_09a0;
:e TP_09b0;

LEXICON 09_S-KSE !vennas
:s TP_09a0;
:kse TP_09b0;

LEXICON 10_0-SE-S	!väike-väikese
 An_SgN;
 :se An_SgG;
 10_;

LEXICON 10_NE-SE-S	!soolane
 :ne An_SgN;
 :se An_SgG;
 10_;

LEXICON 10_KSE		!väike-väikse
 e An_SgN;
 :se An_SgG;
 e 10_;

LEXICON 10_
:s An_SgP_t;
:s An_PlG_te;
:se An_PlP_id;

LEXICON 11		!harjutus
An_SgN;
:e An_SgG;
An_SgP_t;
An_SgAdt_se; 
An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 12_NE-SE-S  !tavaline
:ne An_SgN;
12_;

LEXICON 12_0-SE-S   !kleenuke
An_SgN;
12_;

LEXICON 12_
:se An_SgG;
:si TP_12b0v;
:s TP_12c0;

LEXICON 13	!suur
An_SgN;
:e An_SgG;
An_SgP_t;
An_SgAdt_tte; 
An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 14_S-NE	!laas
:s An_SgN;
:ne$ An_SgG;
:n$ An_SgP_t;
:n$ An_SgAdt_de; 
:n$ An_PlG_te;
:si An_PlP_V;
:si An_Pl_Vok; ! ???

LEXICON 14	!uus
An_SgN;
:e$ An_SgG;
:$ An_SgP_t;
:$ An_SgAdt_de; 
:$ An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 15	!käsi
:i An_SgN;
:e$ An_SgG;
:$ An_SgP_tt;
:$ An_SgAdt_tte;
:$ An_PlG_te;
:i An_PlP_V;
:i An_Pl_Vok;

LEXICON 16	!ratsu
TP_16a0;

LEXICON 16_Adt	!kõne ?? kere, pere, tare
TP_16a0;
:2 An_SgAdt_0;   

LEXICON 16_GRI_Adt	!pogri
:gri TP_16a0;
:kri An_SgAdt_0;   

LEXICON 17	!tuvi
TP_17a0;

LEXICON 17_Adt	!saba, kivi
TP_17a0;
:2 An_SgAdt_0; 

LEXICON 18	!tigu
TP_18at;
:$ TP_18an;

LEXICON 18_Adt	!lagi, ladu, jõgi, lugu 
TP_18at;
:$ TP_18an;
:2 An_SgAdt_0;

LEXICON 18_PlPV	!siga 
TP_18at;
:$ TP_18an;
An_PlP_V;

LEXICON 18_Adt_PlPV	!sõda, rida, tuba
TP_18at;
:$ TP_18an;
:2 An_SgAdt_0;
:+W An_PlP_V;

LEXICON 19	!seminar 
TP_19a0;
:i TP_19b0;

LEXICON 20	!nimi
:i TP_20a0;
:e TP_20b0;

LEXICON 21	!jõgi
18_Adt;

LEXICON 22_A  !jalg, pikk, sepp
An_SgN;
:a$ TP_22bn;
:a TP_22bt;

LEXICON 22_E  !hetk
An_SgN;
:e$ TP_22bn;
:e TP_22bt;

LEXICON 22_I  !tekk
An_SgN;
:i$ TP_22bn;
:i TP_22bt;

LEXICON 22_U  !tikk 
An_SgN;
:u$ TP_22bn; 
:u TP_22bt;

LEXICON 23_A	!hein
TP_23a;
:a$ TP_23bn;
:a TP_23bt;

LEXICON 23_I  !poiss
TP_23a;
:i$ TP_23bn;
:i TP_23bt;

LEXICON 24_A	!padi
:$ An_SgN;
:a$ TP_24bn;
:a TP_24bt;

LEXICON 24_E	!koger
:$ An_SgN;
:e$ TP_24bn;
:e TP_24bt;

LEXICON 24_U	!puder
:$ An_SgN;
:u$ TP_24bn;
:u TP_24bt;

LEXICON 24_I-VE	!tali
:i$ An_SgN;
:ve$ TP_24bn;
:ve TP_24bt;

LEXICON 25	!õnnelik
:$ An_SgN;
:u$ TP_25bn;
:u TP_25bt;

LEXICON 26	!idee
TP_26a0;
An_PlP_id;
:+i An_Pl_Vok;

LEXICON 26_ii	!galerii
TP_26a0;

!*****noomenite lõpp ja verbide algus********

LEXICON 27	! muutumatu tüvega ja astmevahelduseta verbid (kõige produktiivsem verbitüüp (elama)
 TP_27a0; 

LEXICON 28	! ma ja da - TA, olevik -  NA (leppima)
 TP_28at;
 :$ TP_28an;

LEXICON 29	! ma ja olevik - TA, da - NA - (hüppama)
 TP_29at;
 :$ TP_29an;

LEXICON 30	! LE-EL, ma ja olevik - TA, da - NA - (riidlema)
 :le TP_30at;
 :el$ TP_30bn;

LEXICON 31	! ELE, astmevahelduseta - (rabelema)
 :e TP_30at;
 TP_30bn;

LEXICON 32	! ma ja da - TA+konsonanttüvi, olevik - NA+Vokaaltüvi (seisma)
 TP_32at;
 :a$ TP_32bn;

LEXICON 32_E	! tüvevokaal e (tõusma)
 TP_32at;
 :e$ TP_32bn;
 
LEXICON 33	! erinevus eelmisest - da, dud, sin, sime (naerma)
 TP_33at;
 :a$ TP_33bn;
 
LEXICON 33_E  ! tüvevokaal e (kuulma)
 TP_33at;
 :e$ TP_33bn;

LEXICON 34	! ma - TA + kons.tüvi, da - TA, tüvi&formatiiv sulandunud, olevik - NA ja vok.tüvi, umbisik - e+tud
 TP_34at;
 :a$ TP_34bn;
 :e$ TP_34cn;

LEXICON 34_K_TUD ! laskma/mõskma/puskma
 :K TP_34at;
 :Ke$ TP_34bn;
 :$ TP_34cn;
 
LEXICON 34_D_TUD ! hoidma-leidma-püüdma
 :D TP_34at;
 :De$ TP_34bn;
 :$ TP_34cn;
 

LEXICON 35	! 4 tüve: nut+ma (at), nuta+b (bn), nute+tud (cn), nutt+a (ct)
 TP_35at;
 :a$ TP_35bn;
 :e$ TP_35cn;
 TP_35ct;

LEXICON 36	! 5 tüve (tule+ma, tul+i, tul+dud, tull+a, tull+akse)
 :e$ TP_36an;
 :$ TP_36bn;
 TP_36bt;
 :=2 TP_36ct;	!Siin võib-olla alati ei ole l, vaid suvalise konsonandi 2-kordistamine.
 :=2 TP_36cn;

LEXICON 37	! võima (sin,ge,me - NA, teised - TA)
 :$ TP_37an;
 TP_37at;

LEXICON 38	! sööma (6 tüve: an, at, bn, bt, cn, ct)
 38_;
 :$ TP_38cn;	! "sõi(n,me)" tekib ka reeglitest
 TP_38ct;

LEXICON 38_SIN ! (ct tüve ei moodustu?) müüma, pooma, käima, viima
 38_;
 An_sin;

LEXICON 38_
 :$ TP_38an;
 TP_38at;
 :$ TP_38bn; ! tüvevariandi "süü(a)" tekitavad reeglid 
 TP_38bt;

 
! TP_  tüvevariantide paigutusviisid paradigmas

LEXICON TP_02b0  ! õpiku
An_SgG;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON TP_03bt	!vahtra, numbri
An_SgG;
An_SgP_t;
An_PlG_te;
An_PlP_id;

LEXICON TP_04b0-05bt	!aseme (=04), liikme (=05) 
An_SgG;
An_PlG_te;
An_PlP_id;

LEXICON TP_07an  ! hammas
An_SgN;
An_SgP_t;
An_PlG_te;

LEXICON TP_07bt  ! hamba (= 08)
An_SgG;
An_PlP_id;

LEXICON TP_08an  ! kannel
An_SgN;
An_SgP_t;
An_PlG_de;

LEXICON TP_09a0	! katus 
An_SgN;
An_SgP_t;
An_PlG_te;

LEXICON TP_09b0	! katuse
An_SgG;
An_PlP_id;

LEXICON TP_12b0v  ! tavalisi
An_PlP_V;
An_Pl_Vok;

LEXICON TP_12c0  ! tavalis, tasumis
An_SgP_t;
An_SgAdt_se;
An_PlG_te;

LEXICON TP_16a0	!kõne, ratsu
An_SgN;
An_SgG;
An_SgP_t;
An_PlG_de;
An_PlP_sid;

LEXICON TP_17a0	!saba, tuvi
An_SgN;
An_SgG;
An_SgP_0;
An_PlG_de;
An_PlP_sid;
:+W An_PlP_V;	!Mitmuse tüvevokaali määratakse reeglitega.
:+W An_Pl_Vok;

LEXICON TP_18at	!sõda, tigu
An_SgN;
! siin olid kolmed?
An_SgP_0; ! TA muutetüvi 
An_PlG_de;
An_PlP_sid;

LEXICON TP_18an	!sõja, teo
An_SgG;

LEXICON TP_19a0	!seminar
An_SgN;

LEXICON TP_19b0	!seminari
An_SgG;
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
:+W An_PlP_V;	!seminare
:+W An_Pl_Vok;

LEXICON TP_20a0	!nimi
An_SgN;

LEXICON TP_20b0	!nime
An_SgG;
An_SgP_0;
:2 An_SgAdt_0;
An_PlG_de;
An_PlP_sid;

LEXICON TP_22bn	!jala, tiku, sepa
An_SgG;
:+W An_PlP_V;
:$+W An_Pl_Vok;  !sõnal tikk vokaalmitmuse vormid naljakad, aga mõnel sõnal ehk pole?


LEXICON TP_22bt  ! jal´ga, tikku, seppa
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
An_PlP_sid;

LEXICON TP_23a	!hein
An_SgN;
:$ An_PlG_te;

LEXICON TP_23bn	!heina
An_SgG;
:+W An_PlP_V;

LEXICON TP_23bt	!hei´na
An_SgP_0;
An_SgAdt_0;
:+W An_PlP_V;


LEXICON TP_24bn	!padja
An_SgG;
:+W An_Pl_Vok;	!padjus

LEXICON TP_24bt	!patja
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
An_PlP_sid;
:+W An_PlP_V;	!patju

LEXICON TP_25bn	!õnneliku
An_SgG;
:+W An_PlG_V;	!õnnelike, õnnelikes jne.

LEXICON TP_25bt	!õnnelikku
An_SgP_0;
An_SgAdt_0;
An_PlG_de;
An_PlP_sid;
:+W An_PlP_V;	!õnnelikke

LEXICON TP_26a0	!idee, galerii
An_SgN;
An_SgG;
An_SgP_d;
An_PlG_de;
An_PlP_sid;

! noomenite lõpp, verbide algus
LEXICON TP_27a0	!ela+ma
 An_ma;
 An_mata;
 An_v;
 An_s;
 An_sin;
 An_sime;
 An_da;
 An_ge;
 An_b;
 An_me;
 An_tud;
 An_takse;
 Deriv_mine_ja;

LEXICON TP_28at	!leppi+da
 An_ma;
 An_mata;
 An_v;
 An_s;
 An_sin;
 An_sime;
 An_da;
 An_ge;
 Deriv_mine_ja;

LEXICON TP_28an	!lepi+n
 An_b;
 An_me;
 An_tud;
 An_takse;

LEXICON TP_29at	!hüppa+ma
 An_ma;
 An_mata;
 An_v;
 An_s;
 An_sin;
 An_sime;
 An_b;
 An_me;
 Deriv_mine_ja;

LEXICON TP_29an	!hüpa+ta
 An_ta;
 An_ke;
 An_tud;
 An_takse;

LEXICON TP_30at	!riidle+ma; rabele+ma
 An_ma;
 An_mata;
 An_v;
 An_s;
 An_sin;
 An_sime;
 An_b;
 An_me;
 Deriv_mine_ja;

LEXICON TP_30bn	!riiel+da; rabel+da
 An_da;
 An_ge;
 An_dud;
 An_dakse;

! 31. tüübil langeb tüvevariantide a ja b paigutus täpselt kokku 30. tüübiga, ainult 31 on astmevahelduseta.

LEXICON TP_32at	!seis+ma
 An_ma;
 An_mata;
 An_ev;	
 An_is;
 An_in;
 An_ime;
 An_ta;
 An_ke;
 An_tud;
 An_takse;
 Deriv_mine_ja;

LEXICON TP_32bn	!seisa+b
 An_b;
 An_me;

LEXICON TP_33at	!naer+ma
 An_ma;
 An_mata;
 An_ev;	
 An_is;
 An_sin;
 An_sime;
 An_da;
 An_ge;
 An_dud;
 An_dakse;
 Deriv_mine_ja;

LEXICON TP_33bn	!naera+b
 An_b;
 An_me;

LEXICON TP_34at	!saat+ma
 An_ma;
 An_mata;
 An_ev;	
 An_is;
 An_sin;
 An_sime;
 An_a;
 An_ke;
 Deriv_mine_ja;

LEXICON TP_34bn	!saada+b
 An_b;
 An_me;

LEXICON TP_34cn	!saade+tud
 An_tud;
 An_takse;

LEXICON TP_35at	!nut+ma
 An_ma;
 An_mata;
 An_sin;
 An_sime;
 An_ke;
 Deriv_mine_ja;
 
LEXICON TP_35bn	!nuta+b
 An_b;
 An_me;

LEXICON TP_35cn	!nute+tud
 An_tud;
 An_takse;

LEXICON TP_35ct	!nutt+a
 An_a;
 An_ev;
 An_is;

LEXICON TP_36an	!tule+ma
 An_ma;
 An_mata;
 An_v;
 An_b;
 An_me;
 Deriv_mine;

LEXICON TP_36bn	!tul+i
 An_i;
 An_in;
 An_ime;
 An_ge;
 i Deriv_ja;

LEXICON TP_36bt	!tul+dud
 An_dud;

LEXICON TP_36cn	!tull+akse
 An_akse;

LEXICON TP_36ct	!tull+a
 An_a;

LEXICON TP_37an	!või+sin, või+me
 An_mata;
 An_sin;
 An_sime;
 An_ge;
 An_me;

LEXICON TP_37at	!või+ma
 An_ma;
 An_v;
 An_s;
 An_da;
 An_b;
 An_dud;
 An_dakse;
 Deriv_mine_ja;

LEXICON TP_38an	!söö+me
 An_mata;
 An_ge;
 An_me;

LEXICON TP_38at !söö+ma
 An_ma;
 An_v;
 An_b;
 An_dud;

LEXICON TP_38bn	! süü+akse
 An_akse;

LEXICON TP_38bt	!süü+a
 An_a;

LEXICON TP_38cn	!sõime
 An_ime;

LEXICON TP_38ct	!sö.+i -> sõi
 An_i;
 An_in;


! An_  põhivormide ja nende analoogiavormide moodustamine

LEXICON An_SgN  ! Sg Nom
 +sg+nom:0 GI;
 LIIT;

LEXICON An_SgG  ! SgG analoogiarühm
 +sg+gen:0 GI;
 +sg:0 KÄÄNE_1;
 +pl+nom:+d GI;
 +C:+m 02_A;
 +U:+im 02_A;
 +dim:ke 12_0-SE-S;
 +dim:ke 12_NE-SE-S;
 LIIT;
 !LIIT_DEV_G;

LEXICON An_SgP_0  ! SgP (formatiiv 0)
 +sg+part:0 GI;
 !LIIT_DEV_P;

LEXICON An_SgP_t  ! SgP (formatiiv t) 
 +sg+part:+t GI;
 !:+t LIIT_DEV_P;

LEXICON An_SgP_tt  ! SgP (formatiiv tt) 
 +sg+part:+tt GI;
 !:+tt LIIT_DEV_P;

LEXICON An_SgP_d  ! SgP (formatiiv d) 
 +sg+part:+d GI;
 !:+d LIIT_DEV_P;


LEXICON An_SgAdt_0  ! SgAdt (formatiiv 0) 
 +sg+adit:0 GI;

LEXICON An_SgAdt_de  ! SgAdt (formatiiv de) 
 +sg+adit:+de GI;

LEXICON An_SgAdt_se  ! SgAdt (formatiiv se) 
 +sg+adit:+se GI;

LEXICON An_SgAdt_tte ! SgAdt (formatiiv tte) 
 +sg+adit:+tte GI;



LEXICON An_PlP_id  ! PlP (formatiiv id)
 +pl+part:+id GI;
 +pl:+i KÄÄNE_2;

LEXICON An_PlP_sid  ! PlP (formatiiv sid)
 +pl+part:+sid GI;



LEXICON An_PlP_V  ! PlP (vokaallõpp)
 +pl+part:0 GI;

LEXICON An_Pl_Vok  ! vokaalmitmuse paralleelvormid
 +pl:0 KÄÄNE_2;

LEXICON An_PlG_te  ! de-mitmuse (formatiiv te) analoogiarühm
 +pl+gen:+te GI;
 +pl:+te KÄÄNE_1;
 !:+te LIIT_DEV_G;

LEXICON An_PlG_de  ! de-mitmuse (formatiiv de) analoogiarühm 
 +pl+gen:+de GI;
 +pl:+de KÄÄNE_1;
 !:+de LIIT_DEV_G;
 :+de LIIT;

LEXICON An_PlG_V	!õnnelik-tüüp
 +pl+gen: GI;
 +pl: KÄÄNE_1;
! LIIT_DEV_G;
 LIIT;

LEXICON KÄÄNE_1  ! käändelõpud illatiivist komitatiivini
 +ill:+sse GI;
 +in:+s GI;
 +el:+st GI;
 +all:+le GI;
 +ad:+l GI;
 +abl:+lt GI;
 +tr:+ks GI;
 +ter:+ni GI;
 +es:+na GI;
 +ab:+ta GI;
 +kom:+ga GI;

LEXICON KÄÄNE_2  ! käändelõpud illatiivist translatiivini (mitmus)
 +ill:+sse GI;
 +in:+s GI;
 +el:+st GI;
 +all:+le GI;
 +ad:+l GI;
 +abl:+lt GI;
 +tr:+ks GI;

! Analoogiarühmad verbidele
LEXICON An_ma
 ma+V+sup+ill:ma GI;
 ma+V+quot+pres+ps:vat GI;

LEXICON An_mata
 ma+V+sup+ab:mata GI;
 ma+V+sup+in:mas GI;
 ma+V+sup+el:mast GI;
 ma+V+sup+tr:maks GI;

LEXICON An_v
 ma+V+partic+pres+ps:v GI;
 v+A:v 02_A ; ! +A+pos+sg+nom+partic

LEXICON An_ev
 ma+V+partic+pres+ps:ev GI;
 ev+A:Ev 02_A ; ! +A+pos+sg+nom+partic

!!!!!
! Järgmised 7 - kindla kõneviisi lihtmineviku vormid
!!!!!

LEXICON An_s
 ma+V+indic+impf+ps3+sg+ps+af:+s GI;

LEXICON An_is
 ma+V+indic+impf+ps3+sg+ps+af:+is GI;

LEXICON An_i
 ma+V+indic+impf+ps3+sg+ps+af:+i GI;

LEXICON An_sin
 ma+V+indic+impf+ps1+sg+ps+af:+sin GI;
 ma+V+indic+impf+ps2+sg+ps+af:+sid GI;
 ma+V+indic+impf+ps3+pl+ps+af:+sid GI;

LEXICON An_in
 ma+V+indic+impf+ps1+sg+ps+af:+i i_n;
 ma+V+indic+impf+ps2+sg+ps+af:+id GI;
 ma+V+indic+impf+ps3+pl+ps+af:+id GI;

LEXICON i_n ! +in on multichar, teeme katki
 :n GI;

LEXICON An_sime
 ma+V+indic+impf+ps1+pl+ps+af:+sime GI;
 ma+V+indic+impf+ps2+pl+ps+af:+site GI;

LEXICON An_ime
 ma+V+indic+impf+ps1+pl+ps+af:+ime GI;
 ma+V+indic+impf+ps2+pl+ps+af:+ite GI;

!!!!!

!!!!!
! Infinitiivi variandid -da -ta -a
!!!!!

LEXICON An_da
 ma+V+inf:+da GI;
 ma+V+ger:+des GI;

LEXICON An_ta
 ma+V+inf:+ta GI;
 ma+V+ger:+tes GI;

LEXICON An_a
 ma+V+inf:+a GI;
 ma+V+ger:+0es GI;

!!!!!

!!!!!
! Käskiv kõneviis + nud + nuks + nuvat
!!!!!

LEXICON An_ge
 ma+V+imper+pres+ps3+sg+ps+af:+gu GI ; 
 ma+V+imper+pres+ps1+pl+ps+af:+gem GI ; 
 ma+V+imper+pres+ps2+pl+ps+af:+ge GI ; 
 ma+V+imper+pres+ps3+pl+ps+af:+gu GI ;
 An_nud ; 
  
LEXICON An_ke
 ma+V+imper+pres+ps3+sg+ps+af:+ku GI ;
 ma+V+imper+pres+ps1+pl+ps+af:+kem GI ;
 ma+V+imper+pres+ps2+pl+ps+af:+ke GI ;
 ma+V+imper+pres+ps3+pl+ps+af:+ku GI ;
  An_nud ;

LEXICON An_nud
  ma+V+partic+past+ps:+nud GI ;
  $nud+A+pos+partic:+nud GI ; ! +A+pos+partic:+nud 
  $nu+S:nu 01;
  ma+V+cond+partic+past+ps1+sg+af:+nuksin GI ; 
  ma+V+cond+partic+past+ps2+sg+af:+nuksid GI ; 
  ma+V+cond+partic+past+ps3+sg+af:+nuks GI ; 
  ma+V+cond+partic+past+ps1+pl+af:+nuksime GI ; 
  ma+V+cond+partic+past+ps2+pl+af:+nuksite GI ; 
  ma+V+cond+partic+past+ps3+pl+af:+nuksid GI ; 
  ma+V+quot+partic+past+af:+nuvat GI ;
!!!!!

!!!!!
! Kindla kõneviisi olevik
!!!!!

LEXICON An_b
 ma+V+indic+pres+ps1+sg+ps+af:+n GI;
 ma+V+indic+pres+ps2+sg+ps+af:+d GI;
 ma+V+indic+pres+ps3+sg+ps+af:+b GI;
 ma+V+indic+pres+ps+neg:+0 GI;
 ma+V+imper+pres+ps2+sg:+0 GI;
 ma+V+cond+pres+ps1+sg+ps+af:+ksin GI ;
 ma+V+cond+pres+ps2+sg+ps+af:+ksid GI ;
 ma+V+cond+pres+ps3+sg+ps+af:+ksid GI ;
 ma+V+cond+pres+ps1+pl+ps+af:+ksime GI ;
 ma+V+cond+pres+ps2+pl+ps+af:+ksite GI ;
 ma+V+cond+pres+ps3+pl+ps+af:+ksid GI ;

LEXICON An_me
 ma+V+indic+pres+ps1+pl+ps+af:+me GI;
 ma+V+indic+pres+ps2+pl+ps+af:+te GI;
 ma+V+indic+pres+ps3+pl+ps+af:+vad GI;

!!!!!

!!!!!
! Umbisikuline tegumood
!!!!!

LEXICON An_tud
 ma+V+partic+past+imps : +tud GI ;
 $tud+A+pos+partic : +tud GI ;
 $tu+S : +tu 01 ;
 ma+V+indic+impf+imps+af : +ti GI ; 
 ma+V+cond+pres+imps : +taks GI ; 
 ma+V+cond+past+imps : +tuks GI ; 
 ma+V+quot+pres+imps : +tavat GI ; 
 ma+V+quot+past+imps : +tuvat GI ; 
 ma+V+imper+pres+imps : +tagu GI ; 
 ma+V+sup+imps : +tama GI ;
 ma+V+indic+pres+imps+neg : +ta GI ; 
 ma+V+partic+pres+imps : +tav GI ;
 $tav+A : +tav 02_A ;

LEXICON An_dud
 ma+V+partic+past+imps : +dud GI ;
 $dud+A+pos+partic : +dud GI ;
 $du+S : +du 01 ;
 ma+V+indic+impf+imps+af : +di GI ; 
 ma+V+cond+pres+imps : +daks GI ; 
 ma+V+cond+past+imps : +duks GI ; 
 ma+V+quot+pres+imps : +davat GI ; 
 ma+V+quot+past+imps : +duvat GI ; 
 ma+V+imper+pres+imps : +dagu GI ; 
 ma+V+sup+imps : +dama GI ; 
 ma+V+indic+pres+imps+neg : +da GI ; 
 ma+V+partic+pres+imps : +dav GI ;
 $dav+A : +dav 02_A ;

LEXICON An_takse
 ma+V+indic+pres+imps+af : +takse GI ;

LEXICON An_dakse
 ma+V+indic+pres+imps+af : +dakse GI ;

LEXICON An_akse
 ma+V+indic+pres+imps+af : +akse GI ;

!!!!!

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Produktiivne verbituletus (v,tav,nud,nu,tud,tu on analoogiarühmade all)
!!!!!!
LEXICON Deriv_mine_ja
  ja+S : +ja 01;
  mine+S : +mi 12_NE-SE-S;

LEXICON Deriv_ja
  ja+S : +ja 01;

LEXICON Deriv_mine
  mine+S : +mi 12_NE-SE-S;


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! liitsõnade moodustamine

LEXICON LIIT
! #;
:- #; 
+:& Nimisõna;
+:& Omadussõna;
+:& Tegusõna; ! vähemalt tuletiste jaoks on seda vaja.


!LEXICON LIIT_DEV_P
!+:& Deverbaal_P;

!LEXICON LIIT_DEV_G
!+:& Deriv_mine_ja;


LEXICON Numbrid ! need tuleks tegelt eraldi fst'ga teha ja ühendiga lahendada.

<Number+ %+Num:0 > #;
<Number+ %. %+Ord:0 > #;
<Number+ (%-)> KÄÄNE_1; ! -l, -le, -lt tuleks nõuda sidekriipsu!

